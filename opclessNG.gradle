// this should be replaced by opc one day
// is based on mdk with opc applied on top of that

buildscript {
    repositories {
        maven { url = 'https://maven.neoforged.net/releases' }
        maven { url = 'https://maven.parchmentmc.org' }
        flatDir { dirs 'libs' }
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "net.neoforged.gradle:userdev:7.0.+"
    }
}

apply plugin: net.neoforged.gradle.userdev.UserDevPlugin
apply plugin: 'maven-publish'

version = modVersion
group = modGroup

repositories {
    mavenLocal()
}

base {
    archivesName = modId
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'info'
        modSource project.sourceSets.main

        final File gameDir = project.file("run/${it.name}") as File
        gameDir.mkdirs();

        workingDirectory.set gameDir
        //it.programArguments.addAll '--gameDir', "\"${gameDir.absolutePath}\""
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.modId
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.modId
        programArgument '--nogui'
    }

    data {
        programArguments.addAll '--mod', project.modId, '--all', '--output', file('src/datagen/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/datagen/generated/resources' }

dependencies {
    implementation "net.neoforged:neoforge:${forgeVersion}"

}

tasks.withType(ProcessResources).configureEach {
    def props = new HashMap<>(project.properties)
    props.put("version", rootProject.version)

    filesMatching(['META-INF/mods.toml']) {
        expand props
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}